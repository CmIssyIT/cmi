plugins {

    id "org.springframework.boot" version "1.5.7.RELEASE"

    id "org.sonarqube" version "2.6"

}

apply plugin: 'java'
apply plugin: "jacoco"

group = 'fr.cmi'
version = '1.0'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

springBoot {
}

bootRun {
    args = ["--spring.profiles.active=dev"]
}

test {
    include '**/*Test.class'
    testLogging.showStandardStreams = true
}

task verify(type: Test, group: 'verification') {
    include '**/*IT.class'
    testLogging.showStandardStreams = true
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoIT.exec")
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

sonarqube {
    properties {
        property 'sonar.projectName', 'cmi-api'
        property 'sonar.projectKey', 'cmi-api'
        property 'sonar.jacoco.reportPaths', '$buildDir/jacoco/jacocoTest.exec' + ',' + '$buildDir/jacoco/jacocoIT.exec'
        property 'sonar.junit.reportPaths', '$buildDir/test-results/test' + ',' + '$buildDir/test-results/verify'
    }
}

build.dependsOn verify

dependencies {

    // Spring: RestController, Monitoring, Tests
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-actuator')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.boot:spring-boot-starter-validation')

    compile("net.logstash.logback:logstash-logback-encoder:4.6")

    // Swagger: API live-documentation
    compile('io.springfox:springfox-swagger2:2.7.0')
    compile('io.springfox:springfox-swagger-ui:2.7.0')

    // RestAssured: API integration tests
    testCompile 'io.rest-assured:rest-assured:3.0.3'

    // Spring JPA: Database
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.liquibase:liquibase-core')
    compile('org.postgresql:postgresql')
    testCompile('com.h2database:h2')

    // logstash logback
    testCompile('net.logstash.logback:logstash-logback-encoder:4.7')

}
